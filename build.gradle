
plugins {
  id "java"
  id "com.gradle.build-scan" version "3.13.3"

  id "nebula.dependency-lock" version "8.0.0" apply false
  id "nebula.info-dependencies" version "5.1.0" apply false
  id "nebula.contacts" version "5.1.0" apply false
  id "nebula.maven-publish" version "13.6.1" apply false
  id "nebula.javadoc-jar" version "13.6.1" apply false
  id "nebula.source-jar" version "13.6.1" apply false
  id "nebula.gradle-git-scm" version "4.1.6" apply false
  id "nebula.nebula-bintray" version "7.3.1" apply false
  id "org.ajoberstar.git-publish" version "2.1.1" apply false
  id "de.maltsev.gradle.semanticrelease" apply false
}

description = "Set of JUnit Rules & Extensions to easily load data to test your spring-data elasticsearch-based projects"

buildScan {
  termsOfServiceUrl = 'https://gradle.com/terms-of-service'
  termsOfServiceAgree = 'yes'
}

allprojects {
  apply plugin: 'nebula.publish-verification'
  apply plugin: 'eclipse'
  group = "com.github.faisalferoz"
  version = '2.0.0-SNAPSHOT'
  repositories {
    jcenter()
  }
}

subprojects { apply plugin: "java" }

project(":spring-esdata-loader-core") {
  description = "Core implementation of spring-esdata-loader: a library to easily load data to test your spring-data-elasticsearch-based projects"
  apply plugin: 'maven'
}

project(":spring-esdata-loader-junit4") {
  apply plugin: "java-library"
  dependencies {
    api project(":spring-esdata-loader-core")
  }
  description = "JUnit4 implementation of spring-esdata-loader: a library to easily load data to test your spring-data-elasticsearch-based projects"
  apply plugin: 'maven'
}

project(":spring-esdata-loader-junit-jupiter") {
  apply plugin: "java-library"
  dependencies {
    api project(":spring-esdata-loader-core")
  }
  description = "JUnit Jupiter implementation of spring-esdata-loader: a library to easily load data to test your spring-data-elasticsearch-based projects"
  apply plugin: 'maven'
}

project(":spring-esdata-loader-demo") {
  apply plugin: "java"
  dependencies {
    implementation project(":spring-esdata-loader-junit4"), project(":spring-esdata-loader-junit-jupiter")
  }
  description = "Demo project for spring-esdata-loader: a library to easily load data to test your spring-data-elasticsearch-based projects"
}

apply from: "$rootDir/gradle/rootmodule.gradle"
